[mu [(program)]
[def (get-rank!) [∘ [lamof reg!] [-> block!]]]
[def (critical-mask [-> [-> [-> block!]]])
  [lam [(n-ranks num!) (get-rank get-rank!)]
  [lam [(mask [lamof [∘ input! output!]] [-> block!]) (increment num!)]
  [lam [(c0 reg!) (c1 reg!) (do-critical block!)] {
    [get-rank c0]
    [mask c0]
    [/* {Note: for a nontrivial mask, no rank will be active after the beginning.}]
    [repeat n-ranks {
      [if+2 c1 [== {zero} c0] do-critical]
      addi c0 c0 increment
    }]
}]]]]
[def (critical [-> [lamof clobber! clobber! block!]] [-> [-> block!]])
  [lam [(a) (b)]
    [[critical-mask a b] [lam [(_)] {}] -1]]]
[def (single [-> [-> block!]])
  [lam [(get-rank get-rank!)]
  [lam [(c0 reg!) (do-single block!)] {
    [get-rank c0]
    [if+2 c0 [== {zero} c0] do-single]
  }]]]
[def (init) [lam [(nranks-shmem num!) (single-shmem [-> block!])] {
  [single-shmem {a0} [init-allocate {a1}]]
  [single-shmem {a0} [allocate nranks-shmem {a1}]]
  }]]
[def (reduce-shmem [-> [-> block!]])
  [lam [(n-ranks num!) (get-rank get-rank!)]
    [def (chatbuf-offset) 4]
    [lam [
      (op [lamof reg! reg!] [-> block!])
      (value input! output!) (identity num!)
      (c0 clobber!)
      (c1 clobber!)
    ] {
      mv c0 gp
      addi c0 c0 chatbuf-offset
      [get-rank c1]
      slli c1 c1 2
      add c1 c0 c1
      sw value 0(c1)
      li value identity
      [unroll n-ranks [lam [(n)] {
        lw c1 [* 4 n](c0)
        [op value c1]}]]
      }]]]
[def (parfor [-> [-> block!]])
  [lam [(n-ranks num!) (get-rank get-rank!)]
  [lam [
    (counter reg!)
    (stopreg clobber!)
    (start num!)
    (stop num! [>! start])
    (step num! [>! 0])
    (body block!)]
    [def (step') [* step n-ranks]]
    [def (asm,cycles) [for~ counter stopreg step' body]]
    [def (loop) [
      [unsafe-assert-exact-cycles
        [[snd asm,cycles] [/ [- stop start] step']]]
      [fst asm,cycles]]]
    {
      [get-rank counter]
      slli counter counter [log2 step]
      addi counter counter start
      li stopreg stop
      [id loop]
    }]]]
[program]]
