[mu [(program)]
[def (a0) {a0}] [def (a1) {a1}] [def (a2) {a2}] [def (a3) {a3}] [def (a4) {a4}] [def (a5) {a5}] [def (a6) {a6}] [def (a7) {a7}]
[def (t0) {t0}] [def (t1) {t1}] [def (t2) {t2}] [def (t3) {t3}] [def (t4) {t4}] [def (t5) {t5}] [def (t6) {t6}]
[def (s0) {s0}] [def (s1) {s1}] [def (s2) {s2}] [def (s3) {s3}] [def (s4) {s4}] [def (s5) {s5}] [def (s6) {s6}] [def (s7) {s7}] [def (s8) {s8}] [def (s9) {s9}] [def (s10) {s10}] [def (s11) {s11}]
[def (zero) {zero}] [def (ra) {ra}] [def (sp) {sp}] [def (gp) {gp}] [def (tp) {tp}]
[def (condition!) [assertify [lam [(x)] [[hasattr {condition}] x]] {a condition}]]
[def (make-condition)
  [lam [(true-branch frag!) (false-branch frag!)]
  [lam [(rs1 reg!) (rs2 reg!)]
  [[addattr {condition}]
    [lam [(label frag!)]
      [pair {
        [id true-branch] rs1 rs2 label
      } {
        [id false-branch] rs1 rs2 label
      }]]]]]]
[def (<) [make-condition {blt} {bgeu}]]
[def (>=) [make-condition {bgeu} {blt}]]
[def (==) [make-condition {beq} {bne}]]
[def (!=) [make-condition {bne} {beq}]]
[def (+acc [-> block!]) [lam [(acc input! output!) (other input!)] {add acc acc other}]]
[def (-acc [-> block!]) [lam [(acc input! output!) (other input!)] {sub acc acc other}]]
[def (branch~) [lam [(condition condition!) (asm block!)] {
  [[condition {DONE}] false]
  [id asm]
  DONE:
  }]]
[def (unroll) [lam [(n num!) (body [lamof num!] [-> block!])]
  [[[fold-range 1 0 n] {}]
  [lam [(acc) (i)] {
    [id acc]
    [body i]
  }]]]]
[def (join) [lam [(n num!) (glue block!) (body [-> [lamof num!] block!])] {
  [[[fold-range 1 0 [- n 1]] {}]
  [lam [(acc) (i)] {
    [id acc]
    [body i]
    [id glue]
  }]]
  [body [- n 1]]}]]
[def (repeat~) [lam [(n num!) (body block!)]
  [unroll n [lam [(_)] body]]]]
[def (repeat) [lam [(n num!) (body block!)]
  [[unsafe-assert-exact-cycles [* n [cycles? body]]]
    [repeat~ n body]]]]
[def (for~) [lam [
    (start reg!) (stop reg!)
    (step num! [>! 0])
    (body block!)]
  [def (addition-and-branching-cycles) [+ cycles-of-iarith cycles-of-taken-branch]]
  [def (total-cycles [lamof num!]) [lam [(num-iterations)] [-
    [* num-iterations
      [+ [cycles? body] addition-and-branching-cycles]]
    [- cycles-of-taken-branch cycles-of-not-taken-branch]]]]
  [def (assembly) {
    LOOP:
    [id body]
    addi start start step
    blt start stop LOOP
  }]
  [pair assembly total-cycles]]]
[def (for) [lam [
    (counter reg!)
    (clobber clobber!)
    (start num!)
    to (stop num! [>! start])
    by (step num! [>! 0])
    (body block!)]
  [def (stopreg) clobber]
  [def (startup) {
    li counter start
    li stopreg stop
  }]
  [def (num-iterations) [/ [- stop start] step]]
  [def (assembly,cycles pair!) [for~ counter stopreg step body]]
  {
    [id startup]
    [
      [unsafe-assert-exact-cycles
        [[assembly,cycles false] num-iterations]]
      [assembly,cycles true]]
  }]]
[def (prologue-epilogue!) [∘ [lamof [∘ num! [∘ [>=! 0] [<! 12]]]] [-> block!]]]
[def (stackpush) [lam [(s/t/a frag!)] [lam [(n)]
  [[[fold-range 1 0 n] {addi sp sp [* 4 n]}]
    [lam [(acc block!) (k num!)] {
      sw [id s/t/a][id k] [* 4 k](sp)
      [id acc]
    }]]]]]
[def (stackpop) [lam [(s/t/a frag!)] [lam [(n)]
  [[[fold-range 1 0 n] {addi sp sp [* -4 n]}]
    [lam [(acc block!) (k num!)] {
      [id acc]
      lw [id s/t/a][id k] [* 4 k](sp)
    }]]]]]
[def (file) [lam [(function-names frag!) (body block!)]
  [mu []
    [[println {.global [id function-names]}]]
    [[println {.section .text}]]
    [[println body]]
    [[println {.end}]]]]]
[def (prologue prologue-epilogue!) [stackpush {s}]]
[def (epilogue prologue-epilogue!) [stackpop {s}]]
[def (busywait) [lam [(load [-> [lamof output!] block!]) (c0 clobber!)] {
  LOOP:
  [load c0]
  beqz c0 LOOP}]]
[;; { delay for k cycles. }]
[def (delay-short [-> block!]) [lam [(k num!)] [repeat k {nop}]]]

[def (delay [-> block!]) [lam [(c0 clobber!) (k num! [>=! 0])]
  [if [<? k 4]
    [lam [] [delay-short k]]
    [lam [] [[unsafe-assert-exact-cycles k] {
      li c0 [/ [- k 1] 2]
      DELAY_LOOP:
      addi c0 c0 -1
      bnez c0 DELAY_LOOP
      [delay-short [% [- k 1] 2]]
    }]]]]]

[;; { execute block if condition is true in time = (2 + cycles of block) }]
[def (ifelse [-> block!]) [lam [(c0 clobber!) (condition condition!) (do-if block!) (do-else block!)]
  [def (elsecycles) [cycles? do-else]]
  [def (do-if)
    {
      [id do-if]
      [if [<? [cycles? do-if] elsecycles]
        [lam [] [delay c0 [- [- elsecycles [cycles? do-if]] 1]]]
        [lam [] {}]]
    }]
  [def (ifcycles [>=! [- elsecycles 1]]) [cycles? do-if]]
  [def (assembly block!) {
    [[condition {ELSE}] false]
    [id do-if]
    j DONE
    ELSE:
    [id do-else]
    [delay c0 [+ 1 [- ifcycles elsecycles]]]
    DONE:
    }]
  [;; { overhead = time to branch/not branch + time to jump }]
  [[unsafe-assert-exact-cycles [+ 2 ifcycles]] assembly]]]
[def (if+2) [lam [(c0 clobber!) (condition condition!) (instrs block!)] [ifelse c0 condition instrs {}]]]
[def (best)
  [lam [(compare [-> condition!])]
  [lam [(rd output!) (rs1 input!) (rs2 input!)]
  [ifelse {zero} [compare rs1 rs2] {
    mv rd rs1
  } {
    mv rd rs2
  }]]]]
[def (max~ [-> block!]) [best >=]]
[def (min~ [-> block!]) [best <]]

[def (init-allocate [-> block!]) [lam [(clobber0 clobber!)] {
  li clobber0 4
  sw clobber0 0(gp)
}]]
[def (allocate [-> block!])
  [lam [(nwords num!) (ptr output!)] {
    lw ptr 0(gp)
    addi ptr ptr [* 4 nwords]
    sw ptr 0(gp)
    addi ptr ptr [* -4 nwords]
    add ptr ptr gp
  }]]

[;; { iterator abstract data type }]
[def (get-rank!) [∘ [lamof reg!] [-> block!]]]
[def (iterator!) [assertify [lam [(x)] [[hasattr {iterator}] x]] {an iterator}]]
[def (iterator-giver!) [∘ [lamof output!] [-> iterator!]]]
[def (iterable!) [-> iterator-giver!]]
[def (iterator-new [-> iterator!])
  [lam [
    i (init block!)
    n (next block!)
    l (length num!)
    o (output)
    s (split [lamof num! get-rank!] [-> iterator!])
    u (unroll [lamof [divisor! length] block!])
    h (has-next condition!)
  ]
    [[addattr {iterator}]
      [lam [(get)] [get init next length output split unroll has-next]]]]]
[def (it-init)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] init]]
[def (it-next)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] next]]
[def (it-length)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] length]]
[def (it-output)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] output]]
[def (it-split)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] split]]
[def (it-unroll)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] unroll]]
[def (it-has-next)
  [lam [(init) (next) (length) (output) (split) (unroll) (has-next)] has-next]]

[def (subarray-new [-> iterable!])
  [lam [(array input!) of length (length num!) starting (get-rank get-rank!) strided (stride num!)]
  [lam [(c0 clobber!) (c1 clobber!)]
  [lam [(output output!)]
    [def (endreg) c1]
    [def (currentreg) c0]
    [def (init)
      {
        addi endreg array [* 4 length]
        [get-rank currentreg]
        slli currentreg currentreg 2
        add currentreg currentreg array
      }]
    [def (next)
      {
        lw output 0(currentreg)
        addi currentreg currentreg [* 4 stride]
      }]
    [def (split)
      [lam [(n-ranks) (get-rank)]
        [subarray-iterator c0 c1 array of length [/ length k] starting get-rank strided [* stride n-ranks]]]]
    [def (unroll)
      [lam [(k num!) (block block!)]
      {
        [unroll k
          [lam [(n)]
          {
            lw output [* n [* 4 stride]] (currentreg)
            [id block]
          }]]
        addi currentreg currentreg [* 4 [* k stride]]
      }]]
    [def (has-next) [!= currentreg endreg]]
    [iterator-new i init n next l length o output s split u unroll h has-next]]]]]
[def (array-new [-> iterable!])
  [lam [(array input!) of length (length num!)]
    [subarray-new array of length length starting [lam [(r)] { mv r zero }] strided 1]]]
[def (iterate [-> block!])
  [lam [(output) of (iterator [-> iterator!]) (block block!)]
    [def (iterator) [iterator output]]
    [def (repeated block!)
      {
        ITERATE_LOOP_START:
        [iterator it-next]
        [id block]
        [[[iterator it-has-next] {ITERATE_LOOP_START}] true]
      }]
    [def (repeated
        [unsafe-assert-exact-cycles
          [+
            [* [- [iterator it-length] 1] cycles-of-taken-branch]
            cycles-of-not-taken-branch
            [*
              [iterator it-length]
              [+ [cycles? [iterator it-next]] [cycles? block]]]]
        ])
      repeated]
    {
      [iterator it-init]
      [id repeated]
    }]]
[def (rng)
  [lam [(rng)]
  [lam [
      (counter clobber!)
      (state input!)
      (output output!)
      (nbits num!)
      (length num!)
      (init block!)]
    [def (shuffle)
      {
        slli output state 13
        xor state state output
        srai output state 17
        xor state state output
        slli output state 5
        xor state state output
        andi output state 0b[repeattext nbits {1}]
      }]
    [def (init block!)
      {
        [id init]
        [/* { shuffle a few times in case the initial seed was not a complicated enough state. the number of random bits grows exponentially with base 3ish as you shuffle so a few times should suffice for 32 bits }]
        [repeat 3 shuffle]
        li counter [id length]
      }]
    [def (next)
      {
        [id shuffle]
        addi counter counter -1
      }]
    [;; { note: this split function does not guarantee that the multiset of outputs will be the same as if it was not split }]
    [def (split)
      [lam [(n-ranks num!) (get-rank get-rank!)]
        [[rng rng]
          counter state output nbits [/ length n-ranks]
          {
            [get-rank output]
            addi output output 1
            xor state state output
            [id shuffle]
          }]]]
    [def (unroll)
      [lam [(k num!) (block block!)]
        {
          [unroll k [lam [(n)]
            {
              output-next
            }]]
          addi counter counter [- 0 k]
        }]]
    [def (has-next) [!= zero counter]]
    [iterator-new i init n next l length o output s split u unroll h has-next]]]]
[def (rng [-> iterator!]) [rng rng]]
[def (rng-seeded [-> [-> iterator!]])
  [lam [(c0 clobber!) (c1 clobber!)
      bits (nbits) length (length) seeded (seed num!)]
  [lam [(output output!)]
  [rng c0 c1 output nbits length { li c1 seed }]]]]
[def (iterate [-> block!])
  [lam [(output) of (iterator [-> iterator!]) (block block!)]
    [def (iterator) [iterator output]]
    [def (repeated block!)
      {
        ITERATE_LOOP_START:
        [iterator it-next]
        [id block]
        [[[iterator it-has-next] {ITERATE_LOOP_START}] true]
      }]
    [def (repeated
        [unsafe-assert-exact-cycles
          [+
            [* [- [iterator it-length] 1] cycles-of-taken-branch]
            cycles-of-not-taken-branch
            [*
              [iterator it-length]
              [+ [cycles? [iterator it-next]] [cycles? block]]]]
        ])
      repeated]
    {
      [iterator it-init]
      [id repeated]
    }]]
[def (..-with-rank)
  [lam [(..-with-rank)]
  [lam [(stopreg clobber!) (start num!) to (end num! [>=! start]) ranks (n-ranks num!) get-rank (get-rank get-rank!)]
  [def (length') [/ [- end start] n-ranks]]
  [lam [(output)]
    [def (init)
      {
        [get-rank output]
        slli output output [log2 length']
        addi output output [- start 1]
        addi stopreg output length'
      }]
    [def (next) { addi output output 1 }]
    [def (split)
      [lam [(n-ranks num!) (get-rank get-rank!)]
        [[[..-with-rank ..-with-rank] stopreg start to end ranks n-ranks get-rank get-rank] output]]]
    [def (unroll)
      [lam [(k) (block)]
        [unroll k [lam [(n)]
          {
            li output n
            [id block]
          }]]]]
    [def (has-next) [!= output stopreg]]
    [iterator-new i init n next l length' o output s split u unroll h has-next]]]]]
[def (..-with-rank) [..-with-rank ..-with-rank]]
[def (..)
  [lam [(stopreg) (start) to (end)]
    [..-with-rank stopreg start to end ranks 1 get-rank [lam [(r)] { li r 0 }]]]]
[def (cyclify)
  [lam [(cyclify)]
  [lam [(counter clobber!) (c1 clobber!)
      (iterable [-> iterator!]) times (length num!)]
  [lam [(output)]
    [def (iterator) [iterable output]]
    [def (length') [* length [iterator it-length]]]
    [def (init block!)
      {
        li counter length'
        [iterator it-init]
      }]
    [def (next)
      {
        addi counter counter -1
        [ifelse c1 [iterator it-has-next] {
          [iterator it-next]
        } {
          [iterator it-init]
          [iterator it-next]
        }]
      }]
    [def (split)
      [lam [(n-ranks num!) (get-rank get-rank!)]
        [def (splitted) [[iterator it-split] n-ranks get-rank]]
        [[[cyclify cyclify] counter c1 [lam [(_)] splitted] times length] output]]]
    [def (unroll)
      [lam [(k num! [divisor! [iterator it-length]]) (block block!)]
        [[iterator it-unroll] k block]]]
    [def (has-next) [!= zero counter]]
    [iterator-new i init n next l length' o output s split u unroll h has-next]
    ]]]]
[def (cyclify) [cyclify cyclify]]
[def (iterate2)
  [lam [(output0) of (iterable0 [-> iterator!]) and (output1) of (iterable1 [-> iterator!])]
    [def (iterator0) [iterable0 output]]
    [def (iterator1) [iterable1 output]]
    [=! [iterator0 it-length] [iterator1 it-length]]
    [def (repeated block!)
      {
        ITERATE_LOOP_START:
        [iterator0 it-next]
        [iterator1 it-next]
        [id block]
        [[[iterator0 it-has-next] {ITERATE_LOOP_START}] true]
      }]
    [def (repeated
        [unsafe-assert-exact-cycles
          [+
            [* [- [iterator0 it-length] 1] cycles-of-taken-branch]
            cycles-of-not-taken-branch
            [*
              [iterator0 it-length]
              [+ [cycles? [iterator0 it-next]] [cycles? [iterator1 it-next]] [cycles? block]]]]
        ])
      repeated]
    {
      [iterator0 it-init]
      [iterator1 it-init]
      [id repeated]
    }]]
[program]]
